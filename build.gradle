plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.cubecrafter'
version = '1.1.21'

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://simonsator.de/repo/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.cryptomorin:XSeries:9.4.0'
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    implementation 'de.tr7zw:item-nbt-api:2.11.3'
    implementation 'fr.mrmicky:fastboard:2.0.0'
    implementation 'com.github.cubecrafter:XUtils:1.0.13'
    //implementation 'me.cubecrafter:XUtils:1.0.13'

    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'de.simonsator:Party-and-Friends-MySQL-Edition-Spigot-API:1.5.4-RELEASE'
    compileOnly 'de.simonsator:spigot-party-api-for-party-and-friends:1.0.4-RELEASE'
    compileOnly 'de.simonsator:DevelopmentPAFSpigot:1.0.67'
    compileOnly 'org.projectlombok:lombok:1.18.26'

    annotationProcessor 'org.projectlombok:lombok:1.18.26'
}

shadowJar {
    archiveFileName = 'WoolWars-' + project.version + '.jar'

    relocate 'me.cubecrafter.xutils', 'me.cubecrafter.woolwars.libs.xutils'
    relocate 'com.cryptomorin.xseries', 'me.cubecrafter.woolwars.libs.xseries'
    relocate 'com.zaxxer.hikari', 'me.cubecrafter.woolwars.libs.hikari'
    relocate 'org.bstats', 'me.cubecrafter.woolwars.libs.bstats'
    relocate 'de.tr7zw.changeme.nbtapi', 'me.cubecrafter.woolwars.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'me.cubecrafter.woolwars.libs.nbtapi.annotations'
    relocate 'fr.mrmicky.fastboard', 'me.cubecrafter.woolwars.libs.fastboard'

    exclude 'META-INF/**'
    exclude 'LICENSE'

    minimize()
}

tasks.register('buy') {
    doLast {
        println '----------------------------------------------------------------'
        println 'Buy the plugin to get support and keep the development going!'
        println 'SpigotMC: https://www.spigotmc.org/resources/105548/'
        println 'Polymart: https://polymart.org/r/2551'
        println 'BuiltByBit: https://builtbybit.com/resources/25971/'
        println '----------------------------------------------------------------'
    }
}

tasks {
    compileJava {
        options.encoding = 'UTF-8'
        dependsOn clean
    }
    build {
        dependsOn shadowJar
        finalizedBy buy
    }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(8))
}

processResources {
    filteringCharset 'UTF-8'
    expand 'version': project.version
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}